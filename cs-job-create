#!/usr/bin/perl -w
#
# args:
#   <flight> <job> <recipe> <runvar>=<value> ...

use strict qw(vars);
use DBI;
use Osstest;

csreadconfig();

die unless @ARGV >= 2;
my ($flight,$job,$recipe,@runvars) = @ARGV;

#-f "$c{TestingLib}/rc-${recipe}" or die $!;

my $jobsonly= $ENV{OSSTEST_JOBS_ONLY};

exit 0 if grep { $_ eq 'disable' } @runvars;
exit 0 if defined($jobsonly) && !grep { $_ eq $job } split /[:,]/, $jobsonly;

$dbh_tests->
    selectrow_arrayref("SELECT * FROM flights where flight= $flight")
    or die "$flight ?";

our %runvars;
our %suppress;

foreach my $rv (@runvars) {
    if ($rv =~ m/^\!([a-z][0-9a-z_]*)$/) {
        $suppress{$1}= 1;
        next;
    }
    $rv =~ m/^([a-z][0-9a-z_]*)\=(.*)$/ or die "$rv ?";
    my ($name,$val) = ($1,$2);
    next if $suppress{$name};
    $runvars{$name}= $val;
}

db_retry($flight,'constructing', $dbh_tests,[qw(flights)], sub {
    $dbh_tests->do(<<END);
        INSERT INTO jobs VALUES ($flight,'$job','$recipe','queued')
END
    my $q= $dbh_tests->
        prepare("INSERT INTO runvars VALUES ($flight,'$job',?,?,'f')");
    foreach my $name (keys %runvars) {
        $q->execute($name, $runvars{$name});
        $q->finish();
    }
});
