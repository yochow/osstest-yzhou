#!/usr/bin/perl -w

use strict qw(vars);
use DBI;
use Osstest;
use POSIX;

readconfig();
opendb_state();
our ($whhost) = @ARGV;
$whhost ||= 'host';
our $ho= selecthost($whhost);
exit 0 if $ho->{SharedReady};

sub lvextend () {
    my $vg=
        $ho->{Suite} =~ m/lenny/
        ? "$ho->{Name}.$c{TestHostDomain}"
        : $ho->{Name};

    target_cmd_root($ho, <<END);
        set -ex; if ! test -f /root/swap_osstest_enabled; then
            lvcreate -L 10G -n swap_osstest_build $vg ||:
            mkswap /dev/$vg/swap_osstest_build ||:
            swapon /dev/$vg/swap_osstest_build
            touch /root/swap_osstest_enabled
        fi
END

    my $lv= "/dev/$vg/root";

    my $more_pe= 0;

    my $pvinfos= target_cmd_output_root($ho, "pvdisplay --colon");
    my $stripe_minfree;
    my $stripe_count=0;
    foreach my $pvinfo (split /\n/, $pvinfos) {
        my @pvinfo= split /\:/, $pvinfo;
        @pvinfo >= 10 or die "$pvinfo ?";
        my $pv_name= $pvinfo[0];  $pv_name =~ s/^\s+//;
        my $pv_vg= $pvinfo[1];
        my $pv_free_pe= $pvinfo[9];
        if ($pv_vg ne $vg) {
            logm("disk extend: $pv_name: vg $pv_vg != $vg");
            next;
        }
        logm("disk extend: $pv_name: $pv_free_pe free PEs");
        next unless $pv_free_pe;
        if (!defined($stripe_minfree) ||
            $pv_free_pe < $stripe_minfree) {
            $stripe_minfree= $pv_free_pe;
        }
        $stripe_count++;
    }
    $stripe_minfree=0 if !defined $stripe_minfree;
    logm("disk extend: $stripe_count stripe(s) x $stripe_minfree PEs");
    if ($stripe_minfree && $stripe_count>1) {
        my $stripes_free = $stripe_count * $stripe_minfree;
        $more_pe += $stripes_free;
        target_cmd_root($ho, "lvextend -i$stripe_count -l +$stripes_free $lv");
    }

    my $vginfo= target_cmd_output_root($ho, "vgdisplay --colon $vg");
    my @vginfo= split /\:/, $vginfo;
    @vginfo >= 17 or die "$vginfo ?";
    my $pe_size= $vginfo[12];
    my $vg_free_pe= $vginfo[15];

    if ($vg_free_pe) {
        logm("disk extend: unstriped $vg_free_pe PEs");
        $more_pe += $vg_free_pe;
        target_cmd_root($ho, "lvextend -i1 -l +$vg_free_pe $lv");
    }

    my $timeout = 2000 + int($pe_size * 0.000003 * $more_pe);
    logm("disk extend: ${pe_size}k x $more_pe (timeout=$timeout)");
    target_cmd_root($ho, "resize2fs $lv", $timeout);
}

sub prep () {
    target_install_packages($ho,
                            qw(mercurial
                               build-essential bin86 bcc iasl
                               flex bison
                               libpci-dev libncurses5-dev libssl-dev python-dev
                               libx11-dev git-core uuid-dev gettext gawk
                               libsdl-dev libyajl-dev libaio-dev
                               libglib2.0-dev pkg-config
			       ccache));

    target_cmd_root($ho, "chmod -R a+r /usr/share/git-core/templates");
    # workaround for Debian #595728

    if ($ho->{Suite} !~ m/lenny/) {
        target_putfilecontents_stash($ho,60,<<'END','/home/osstest/.hgrc');
[extensions]
mq=
END
    }
}

sub cacheing_git_mount () {
    my $cache= $c{GitCache};
    return unless defined $cache;

    if ($cache =~ m,/$,) {
        my $cachelocal= $c{GitCacheLocal};
        my $subdir= $ho->{Name};
        if (defined $cachelocal) {
            my $umask= umask 0; # yes really
            mkdir "$cachelocal/$subdir", 02777
                or $!==&EEXIST or die "$cachelocal/$subdir $!";
            umask $umask;

            my $cachelock_dir= "/var/run/git-cache";
            my $cachelock_real= "$cachelock_dir/lock";
            target_cmd_root($ho, <<END);
                set -e
                umask 0
                mkdir -p $cachelock_dir
                true >>$cachelock_real
END
            my $cachelock_link= "$cachelocal/$subdir/lock";
            if (lstat $cachelock_link) {
                die "$cachelock_link is not a symlink" unless -l _;
            } else {
                symlink $cachelock_real, $cachelock_link
                    or die "$cachelock_link $!";
            }
        }
        $cache.= $subdir;
    }

    my $mountpoint= '/volatile/git-cache';

    my $mounts= target_cmd_output_root($ho, "mount");
    return if $mounts =~ m/^\S+ on \Q$mountpoint\E /om;

    target_cmd_root($ho, "mkdir -vp $mountpoint");
    target_editfile_root($ho, '/etc/fstab', sub {
        while (<EI>) {
            next if m/^\S+\s+\Q$mountpoint\E\s/o;
            print EO or die $!;
        }
        printf EO "%s %s nfs rw\n", $cache, $mountpoint or die $!;
    });

    my $mount_retries=10;
    # mount can randomly fail due to upstream kernel bug (Debian #636306)
    # this is a shonky workaround
    for (;;) {
        last if eval {
            target_cmd_root($ho, "mount $mountpoint");
            1;
        };
        logm("warning: failed to mount $mountpoint, trying again: $@");
        die unless --$mount_retries > 0;
        target_cmd_root($ho, "sync");
        sleep(2);
    }
}

sub cacheing_git_install () {
    target_install_packages($ho, qw(chiark-utils-bin));
    target_putfile_root($ho,10, "cacheing-git","/usr/local/bin/git", "-p");
}

sub ccache_setup () {
    my $opts= get_host_property($ho, 'build ccache opts', '');
    if (length $opts) {
        target_cmd($ho, "ccache $opts");
    }
}

lvextend();
prep();
cacheing_git_mount();
cacheing_git_install();
ccache_setup();
resource_shared_mark_ready($ho->{Ident}, $ho->{Name},
                           "build-".$ho->{Suite}."-".$r{arch});
