#!/usr/bin/perl -w
#
# Ensure there's enough free space

use strict qw(vars);
use Osstest;
use Fcntl qw(:flock);

our $dryrun= 0;

while (@ARGV && $ARGV[0] =~ m/^\-/) {
    $_= shift @ARGV;
    last if $_ eq '--';
    if (m/^-n$/) {
        $dryrun= 1;
    } else {
	die "$_ ?";
    }
}

die if @ARGV;

csreadconfig();

open LOCK, "> $c{GlobalLockDir}/publish-lock" or die $!;
flock LOCK, LOCK_EX or die $!;

chdir $c{Logs} or die $c{Logs};
$|=1;

my $chkq= $dbh_tests->prepare("SELECT * FROM flights WHERE flight=?");
our @flights;
our @sshopts= qw(-o batchmode=yes);

for (;;) {
    open P, "df --block-size=1M -P . |" or die $!;
    $_= <P>;
    m/^filesystem/i or die "$_ ?";
    $_= <P>;
    m,^\S+\s+\d+\s+\d+\s+(\d+)\s+, or die "$_ ?";
    $!=0; $?=0; close P or die "$! $?";
    my $space= $1;
    printf "space: %8d, wanted: %8d ", $space, $c{LogsMinSpaceMby};
    last if $space >= $c{LogsMinSpaceMby};

    if (!@flights) {
        opendir D, "." or die $!;
        $!=0;
        @flights = sort { $b <=> $a } grep { m/^\d+$/ } readdir D;
        die if $!;
        closedir D or die $!;
        printf "(%d flights) ", scalar @flights;
        die unless @flights;
    }
    my $flight = pop @flights;
    printf "selected %s ", $flight;

    $chkq->execute($flight);
    my $row= $chkq->fetchrow_hashref();
    $chkq->finish();
    die $flight unless defined $row;
    my $age= time - $row->{started};

    printf "(age %dd) ", $age / 86400;

    die "age $age" if $age < $c{LogsMinExpireAge};

    $c{LogsPublish} =~ m/\:/ or die "$c{LogsPublish} ?";
    my ($pubhost, $pubdir) = ($`,$');

    printf "...";

    my $spawn= sub {
        $!=0; my $r= system @_; die "@_ $r $!" if $r;
    };
    $spawn->(qw(ssh -n), @sshopts, $pubhost, <<END);
             set -e
             cd $pubdir
             test -e $flight || exit 0
             printf remote...
             rm -rf $flight
END

    printf "local..";
    $spawn->(qw(rm -rf), $flight);

    printf "done.\n";
    exit 0 if $dryrun;
}

printf "ok.\n";
exit 0;
